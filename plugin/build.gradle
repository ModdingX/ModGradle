plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()
    maven { url = 'https://maven.moddingx.org' }
    maven { url = 'https://maven.minecraftforge.net' }
    maven { url = 'https://maven.parchmentmc.org' }
}

dependencies {
    api('com.google.code.findbugs:jsr305') { version { strictly '[3.0.2,)'; prefer '3.0.2' } }
    api('org.apache.maven:maven-artifact') { version { strictly '[3.9.2,)'; prefer '3.9.2' } }
    api('org.apache.maven:maven-repository-metadata') { version { strictly '[3.9.2,)'; prefer '3.9.2' } }
    api('net.minecraftforge:srgutils') { version { strictly '[0.5.1,)'; prefer '0.5.1' } }
    api('net.minecraftforge.gradle:ForgeGradle') { version { strictly "[6.0.6,6.2)"; prefer '6.0.6' } }
    api('net.minecraftforge:artifactural') { version { strictly '[3.0.14,)'; prefer '3.0.14' } }
    compileOnly('org.parchmentmc:librarian') { version { strictly '[1.2.0,)'; prefer '1.2.0' } }
    api('de.siegmar:fastcsv') { version { strictly '[2.2.2,)'; prefer '2.2.2' } }
    api('com.google.code.gson:gson') { version { strictly '[2.10.1,)'; prefer '2.10.1' } }
    api('com.google.guava:guava') { version { strictly '[32.0.1-jre,)'; prefer '32.0.1-jre' } }
    api('org.apache.commons:commons-lang3') { version { strictly '[3.12.0,4.0.0)'; prefer '3.12.0' } }
    api('commons-io:commons-io') { version { strictly '[2.13.0,)'; prefer '2.13.0' } }
    api('org.moddingx:CurseWrapper') { version { strictly '[3.2,4.0)'; prefer '3.2' } }
    api('org.moddingx:LauncherLib') { version { strictly '[1.1.0,1.2.0)'; prefer '1.1.0' } }
}

gradlePlugin {
    plugins {
        mapping {
            id = 'org.moddingx.modgradle.mapping'
            implementationClass = 'org.moddingx.modgradle.plugins.mapping.MappingPlugin'
        }
        javadoc {
            id = 'org.moddingx.modgradle.javadoc'
            implementationClass = 'org.moddingx.modgradle.plugins.javadoc.JavadocPlugin'
        }
        meta {
            id = 'org.moddingx.modgradle.meta'
            implementationClass = 'org.moddingx.modgradle.plugins.meta.MetaPlugin'
        }
        sourcejar {
            id = 'org.moddingx.modgradle.sourcejar'
            implementationClass = 'org.moddingx.modgradle.plugins.sourcejar.SourceJarPlugin'
        }
        cursedep {
            id = 'org.moddingx.modgradle.cursedep'
            implementationClass = 'org.moddingx.modgradle.plugins.cursedep.CurseDepPlugin'
        }
        packdev {
            id = 'org.moddingx.modgradle.packdev'
            implementationClass = 'org.moddingx.modgradle.plugins.packdev.PackDevPlugin'
        }
        coremods {
            id = 'org.moddingx.modgradle.coremods'
            implementationClass = 'org.moddingx.modgradle.plugins.coremods.CoreModsPlugin'
        }
        mcupdate {
            id = 'org.moddingx.modgradle.mcupdate'
            implementationClass = 'org.moddingx.modgradle.plugins.mcupdate.McUpdatePlugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = rootProject.group
            artifactId = rootProject.name
            version = rootProject.version
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url rootProject.file('../moddingx/maven')
        }
    }
}
